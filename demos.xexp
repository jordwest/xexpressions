
Alias: Byte
	Select:
		Case: 250 or more
			'25[0-5]'
		Case: 200 to 249
			'2[0-4][0-9]'
		Case: Under 200
			'[01]?[0-9]?[0-9]'

XExpression: IP Address
	Description: Matches a valid IPv4 address
	Example:
		Match: 010.001.001.001
		Match: 192.168.000.001
		Match: 255.255.255.000
		Match: 018.64.001.003
		Match: 192.168.0.1
		Match: 10.1.1.1
		Match: 127.0.0.1
		Match: 0.1.5.0
		Non Match: 360.1.5.0
		Non Match: a.b.c.d
		Non Match: aa.bb.00.ff
	Word Boundary
	Group[Capture]: Byte 1
		Byte
	'.'
	Group[Capture]: Byte 2
		Byte
	'.'
	Group[Capture]: Byte 3
		Byte
	'.'
	Group[Capture]: Byte 4
		Byte
	Word Boundary

Alias: Date Separator
	Description: A date separator
	'[- /.]'

XExpression: Date
	Description: This matches a valid date between year 1900 and 2099
	Example:
		Match: 2015-02-21
		Match: 2015/02/21
		Match: 2015 02 21
		Non Match: 3056-02-16
		Non Match: 2015-2-8
		Non Match: 2015-05-34
		Non Match: 2015-13-05

	Group[Capture]: Year
		'(19|20)': Must be in range 1900-2099
		Digit
		Digit
	Date Separator
	Group[Capture]: Month
		Select:
			Case: Jan to Sept
				'0[1-9]'
			Case: Oct to Dec
				'1[012]'
	Date Separator
	Group[Capture]: Day
		Select:
			Case: 1st to 9th
				'0[1-9]'
			Case: 10th to 29th
				'[12][0-9]'
			Case: 30th to 31st
				'3[01]'
